/**
 * Модуль, реализующий front-end, включает стили, логику и графические ресурсы.
 * Модуль полностью отделен от серверного java-кода. Взаимодействие с
 * сервером осуществляется только по согласованному API. Никаких jsp, библиотек тэгов и т.д.
 */
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.eriwen:gradle-js-plugin:$gradleJsPluginVersion"
        classpath "com.eriwen:gradle-css-plugin:$gradleCssPluginVersion"
    }
}

apply plugin: 'css'

/** Компиляция less-файлов */
lesscss {
    source = fileTree("src/css") {
        include 'style.less'
    }
    dest = "$buildDir/$staticContentDir/css"
}
/** Копирование статичного контента без обработки */
task copyStatic(type: Copy) {
    from ('src') {
        include 'aplana/**'
        include 'css/*.css'
        include 'css/fonts/*.*'
        include 'img/**'
        include 'js/**'
        include 'index.html'
    }
    into "$buildDir/$staticContentDir"
}

task clean(type: Delete) {
    delete "$buildDir"
}

task build {
    dependsOn copyStatic, lesscss
}